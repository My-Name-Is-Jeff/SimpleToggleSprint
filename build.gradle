buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        // search through the 'libs' folder
        flatDir dirs: 'libs'
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:86b2392"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "com.github.LxGaming:MixinGradle:0.6-SNAPSHOT" //A custom fork of SpongeMixin that works with Gradle 6
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.1"
group = "mynameisjeff.simpletogglesprint"
archivesBaseName = "SimpleToggleSprint"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=mynameisjeff.simpletogglesprint.tweaker.FMLLoadingPlugin"
    clientRunArgs += "--mixin mixins.simpletogglesprint.json"
}

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
}


dependencies {
    compile("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    provided files("libs/modcore.jar")
}

mixin {
    add sourceSets.main, "mixins.simpletogglesprint.refmap.json"
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePlugin": "mynameisjeff.simpletogglesprint.tweaker.FMLLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.simpletogglesprint.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
    )
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
    }


    exclude 'module-info.class'
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    exclude 'modcore.jar'

    classifier = "-working"
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

task runClientFix {
    doLast {
        String fileName = "${archivesBaseName}-${version}-${mixinClassifier}.jar"
        ant.move file: "${buildDir}/libs/${fileName}", tofile: "${projectDir}/run/mods/${fileName}"
        ant.delete file: "${buildDir}/libs/${archivesBaseName}-${version}.jar"
    }
}

runClient {
    standardInput = System.in
}

build.dependsOn(shadowJar)
runClient.dependsOn(build)
runClient.dependsOn(runClientFix)

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }