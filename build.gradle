buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
        classpath "net.minecraftforge.gradle:ForgeGradle:4.1.+"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

version = "1.0"
group = "mynameisjeff.simpletogglesprint"
archivesBaseName = "SimpleToggleSprint-1.12.2"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    mappings channel: "stable", version: "39-1.12"
    runs {
        client {
            workingDirectory project.file("run")
            args = ["--mixin mixins.simpletogglesprint.json"]
            jvmArgs = []

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            property "mixin.debug.export", "true"
            property "mixin.hotSwap", "true"
            property "fml.coreMods.load=mynameisjeff.simpletogglesprint.tweaker.FMLLoadingPlugin", "true"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-public/" }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"

    include("org.spongepowered:mixin:0.8.3-SNAPSHOT") {
        transitive = false
    }
    annotationProcessor("org.spongepowered:mixin:0.8.3-SNAPSHOT:processor")

    include("gg.essential:Vigilance:123-11202-SNAPSHOT")
}

mixin {
    add sourceSets.main, "mixins.simpletogglesprint.refmap.json"
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePlugin": "mynameisjeff.simpletogglesprint.tweaker.FMLLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.simpletogglesprint.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
    )

    enabled = false
}

reobf {
    shadowJar {}
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.include]
    exclude "LICENSE.md"
    exclude "dummyThing"
    exclude "**/module-info.class"
    exclude "*.so"
    exclude "*.dylib"
    exclude "*.dll"
    exclude "*.jnilib"
    exclude "ibxm/**"
    exclude "com/jcraft/**"
    exclude "org/lwjgl/**"
    exclude "net/java/**"

    exclude "META-INF/proguard/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/com.android.tools/**"

    exclude "fabric.mod.json"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": "1.12.2"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

//jar.finalizedBy("reobfJar")
//tasks.reobfJar.dependsOn(tasks.shadowJar)